apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'com.github.triplet.play'
apply plugin: 'io.fabric'

Properties local_properties = new Properties()
File localPropertiesFile = project.rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    local_properties.load(localPropertiesFile.newDataInputStream())
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.tasomaniac.openwith"
        minSdkVersion 15
        targetSdkVersion rootProject.ext.compileSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""

        buildConfigField "String", "ANALYTICS_KEY", local_properties.analytics_key != null ?
                "\"" + local_properties.analytics_key + "\"" : "\"\""

        resConfigs "ar", "de", "en", "es", "in", "it", "ja", "pt-rBR", "pt-rPT", "ru", "tr"
    }

    signingConfigs {
        debug {
            storeFile file("../distribution/debug.keystore")
        }

        release {
            storeFile file("../distribution/" + (local_properties.release_keystore_name ?: "debug.keystore"))
            keyAlias(local_properties.release_keystore_alias ?: "androiddebugkey")
            storePassword(local_properties.release_keystore_pwd ?: "android")
            keyPassword(local_properties.release_keystore_pwd2 ?: "android")
        }
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            versionNameSuffix "-debug"
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        floss {
            applicationIdSuffix ".floss"
            versionName rootProject.ext.versionName + "-floss"
        }
        prod
    }

    lintOptions {
        abortOnError true
        textReport true
        textOutput 'stdout'

        fatal 'UnusedResources'
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = rootProject.ext.preDexEnabled && !rootProject.ext.ci
    }
}

play {
    serviceAccountEmail = local_properties.play_service_account ?: ''
    pk12File = file("../distribution/" + (local_properties.play_key_file ?: 'no-file.p12'))
    track = 'beta'
}

dependencies {
    def supportLibVersion = "24.2.0"
    def schematicVersion = '0.6.7'
    def butterKnifeVersion = '8.2.1'

    compile "com.android.support:support-v4:$supportLibVersion"
    compile "com.android.support:appcompat-v7:$supportLibVersion"
    compile "com.android.support:recyclerview-v7:$supportLibVersion"
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:preference-v14:$supportLibVersion"

    compile 'com.tasomaniac:delayed-progress:0.3'

    compile 'com.karumi:headerrecyclerview:1.1.0'
    compile 'jp.wasabeef:recyclerview-animators:2.2.3'

    compile 'de.cketti.mailto:email-intent-builder:1.0.0'

    apt "net.simonvt.schematic:schematic-compiler:$schematicVersion"
    compile "net.simonvt.schematic:schematic:$schematicVersion"

    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"
    compile 'com.jakewharton.timber:timber:4.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    prodCompile "com.google.android.gms:play-services-analytics:9.4.0"
    prodCompile 'com.crashlytics.sdk.android:crashlytics:2.6.2'

    testCompile 'junit:junit:4.12'
}
