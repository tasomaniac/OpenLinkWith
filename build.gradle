buildscript {
  ext.ci = 'true' == System.getenv('CI')
  ext.isPlay = project.hasProperty('play')

  def versionMajor = 1
  def versionMinor = 8
  def versionBuild = 3

  ext.versions = [
      compileSdk : 27,
      minSdk     : 19,
      versionCode: versionMajor * 10000 + versionMinor * 1000 + versionBuild,
      versionName: "${versionMajor}.${versionMinor}",

      arch       : '1.1.0',
      butterKnife: '8.8.1',
      dagger     : '2.15',
      kotlin     : '1.2.31',
      okHttp     : '3.10.0',
      schematic  : '0.7.0',
      room       : '1.1.0-alpha1',
      supportLib : '27.1.0',
  ]

  repositories {
    gradlePluginPortal()
    google()
    jcenter()
    if (isPlay) {
      maven { url 'https://maven.fabric.io/public' }
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.0-alpha08'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'com.github.triplet.gradle:play-publisher:1.2.0'
    classpath 'com.novoda:gradle-android-command-plugin:2.0.1'
    classpath 'com.novoda:gradle-build-properties-plugin:0.4'
    classpath 'com.novoda:gradle-static-analysis-plugin:0.5.2'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
    if (isPlay) {
      classpath 'io.fabric.tools:gradle:1.25.1'
    }
  }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
  repositories {
    google()
    jcenter()
  }
}
