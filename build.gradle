// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.4.0-beta5'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.11.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.triplet.gradle:play-publisher:1.1.3'

        classpath 'io.fabric.tools:gradle:1.20.1'
    }
}

apply plugin: 'com.github.ben-manes.versions'

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
}

ext {
    isTravis = "true".equals(System.getenv("TRAVIS"))
    preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

    versionMajor = 1
    versionMinor = 0
    versionPatch = 0
    versionBuild = 1

    compileSdkVersion = 23
    buildToolsVersion = "23.0.1"

    versionCode = versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
    versionName = "${versionMajor}.${versionMinor}" + ((versionPatch != 0) ? ".${versionPatch}" : "")
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
